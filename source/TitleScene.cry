import Audio;

class TitleScene : AbstractScene {
	static field BUTTONS = {
		'play': [120, 288, 330, 436],
		'options': [62, 483, 343, 549],
	};
	
	static field titleMusic = null;
	field shown = false;
	
	constructor() : base() { }
	
	function update(input) {
		if (input.pressedThisFrame) {
			switch (this.detectButton(input.x, input.y) ?? 'none') {
				case 'play':
					GameSession.ACTIVE_SESSION = new GameSession();
					TitleScene.titleMusic.fadeOut(2.0);
					this.switchScene(new SpaceScene(0, 9000.0, 9000.0));
					break;
				
				case 'options':
					this.switchScene(new OptionsScene());
					break;
					
				default:
					break;
			}
		}
	}
	
	function detectButton(x, y) {
		buttons = TitleScene.BUTTONS;

		for (key : buttons.keys()) {
			coords = buttons[key];
			if (x > coords[0] && x < coords[2] &&
				y > coords[1] && y < coords[3]) {
				return key;
			}
		}
		return null;
	}
	
	function render(rc) {
		if (!this.shown) {
			this.shown = true;
			if (TitleScene.titleMusic == null) {
				TitleScene.titleMusic = Music.loadFromResource('music/title.ogg');
			}
			
		}
		
		if (Music.getCurrent() == null) {
			TitleScene.titleMusic.play(true);
		}
		
		title = ImageLibrary.get('title.png');
		title.draw(0, 0);
	}
}